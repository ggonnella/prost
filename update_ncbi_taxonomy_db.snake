#!/usr/bin/env python3

include: "common.snake"

rule all:
  input:
    path.tables/"done.index.ncbi_taxonomy"

rule insert_all:
  input:
    expand(path.tables/"done.insert.ncbi_taxonomy.{table}",
        table=["names", "nodes", "gencode", "division",
               "merged", "delnodes", "citations"]),
  output:
    done=touch(path.tables/"done.insert.ncbi_taxonomy")

rule index_all:
  input:
    rules.insert_all.output.done,
    expand(path.tables/"done.index.ncbi_taxonomy.{table}",
        table=["names", "nodes", "merged"])
  output:
    done=touch(path.tables/"done.index.ncbi_taxonomy")

rule create_table:
  input:
    socket=ancient(path.socket),
    schema=srcdir("scripts/dbschema/ncbi_taxonomy_db.py"),
    dump=path.tax/"{table}.dmp"
  params:
    table="nt_{table}",
    drop=True
  output:
    done=touch(path.tables/"done.create_tables.ncbi_taxonomy.{table}")
  script: "scripts/db_create_tables.py"

rule insert:
  input:
    path.tables/"done.create_tables.ncbi_taxonomy.{table}",
    socket=ancient(path.socket),
    dump=path.tax/"{table}.dmp"
  params:
    table="nt_{table}",
  output:
    done=touch(path.tables/"done.insert.ncbi_taxonomy.{table}")
  script: "scripts/ncbi_taxonomy_db_bulk_insert.py"

rule index_table:
  input:
    path.tables/"done.insert.ncbi_taxonomy.{table}",
    socket=ancient(path.socket)
  params:
    table="nt_{table}"
  output:
    done=touch(path.tables/"done.index.ncbi_taxonomy.{table}")
  script: "scripts/ncbi_taxonomy_db_create_indices.py"

rule get_subtree:
  input:
    rules.index_all.output.done,
    socket=ancient(path.socket)
  params:
    root=config.get("root")
  script: "scripts/ncbi_taxonomy_db_extract_subtree.py"
