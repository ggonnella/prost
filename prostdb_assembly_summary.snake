#!/usr/bin/env python3
"""
Create and update a database containing
the information from the NCBI "assembly_summary" tables.
"""

include: "common.snake"

rule all:
  input: path.tables/"done.insert.ncbi_assembly_summary"

rule insert_all:
  input:
    expand(path.tables/"done.insert.ncbi_assembly_summary.{db}.{domain}",
        db=["genbank", "refseq"], domain=["bacteria", "archaea"])
  output:
    touch(path.tables/"done.insert.ncbi_assembly_summary")

rule create_tables:
  input:
    socket=ancient(path.socket),
    schema=srcdir("scripts/dbschema/ncbi_assembly_summary.py")
  output:
    touch(path.tables/"done.create_tables.ncbi_assembly_summary")
  script:
    "scripts/db_create_tables.py"

rule insert:
  input:
    ancient(rules.create_tables.output),
    socket=ancient(path.socket),
    datasrc=path.asmsummary/"assembly_summary_{db}_{domain}.txt"
  output:
    touch(path.tables/"done.insert.ncbi_assembly_summary.{db}.{domain}")
  script:
    "scripts/ncbi_assembly_summary_bulk_insert.py"
