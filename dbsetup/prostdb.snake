#!/usr/bin/env python3

include: srcdir("../snakes/read_config.snake")

import getpass
sysuser = getpass.getuser()

from pathlib import Path

help_dbpath = "The 'dbpath' configuration variable must contain the path to "+\
             "the database data directory."
help_dbrootpass = "The 'dbrootpass' configuration variable must contain the "+\
             "password of the database administrator account "+\
             f"'{sysuser}'."

check_config_var(config, "dbpath", help_dbpath)
check_config_var(config, "dbrootpass", help_dbrootpass)

help_rules = """\
Select one of the following rules by passing the rule name as an argument
to the snakemake command line.

  initialize: initialize the database data directory and setup the password
              for the database administrator account ('{sysuser}')
  create:     create the database and a database user account
  start:      start the database server
  stop:       stop the database server

Please refer to the documentation for further information.
""".format(sysuser=sysuser)

rule all:
  run:
    print(help_rules)

## Database initialization/creation

rule initialize:
  shell:
    """
    ./install_database.sh {config[dbpath]} {config[dbrootpass]}
    """

rule create:
  output:
    dbdir=directory(Path(config["dbpath"])/config["dbname"])
  shell:
    """
    ./create_database.sh {config[dbpath]} {config[dbrootpass]} \
                         {config[dbname]} {config[dbuser]} \
                         {config[dbpass]}
    """

rule create_test:
  output:
    dbdir=directory(Path(config["dbpath"])/config["testdbname"])
  shell:
    """
    ./create_database.sh {config[dbpath]} {config[dbrootpass]} \
                         {config[testdbname]} {config[dbuser]} \
                         {config[dbpass]}
    """

## Database administration tasks

rule start:
  output:
    socket=config["dbsocket"]
  shell:
    """
    ./start_database_server.sh {config[dbpath]} {config[dbhost]} \
                               {config[dbport]} {output.socket} \
                               {config[dbname]} {config[dbuser]} \
                               {config[dbpass]}
    """

rule stop:
  input:
    socket=ancient(rules.start.output.socket)
  shell:
    """
    mysqladmin -u {sysuser} --password={config[dbrootpass]} \
                --socket={input.socket} shutdown
    """

rule ps:
  input:
    socket=ancient(rules.start.output.socket)
  shell:
    """
    mysqladmin processlist -u {sysuser} \
        --password={config[dbrootpass]} \
        --socket={input.socket}
    """

rule connect:
  input:
    socket=ancient(rules.start.output.socket)
  shell:
    """
    mysql --socket={input.socket} \
          --database={config[dbname]} \
          -u {config[dbuser]} \
          --password={config[dbpass]}
    """

rule tconnect:
  input:
    socket=ancient(rules.start.output.socket)
  shell:
    """
    mysql --socket={input.socket} \
          --database={config[testdbname]} \
          -u {config[dbuser]} \
          --password={config[dbpass]}
    """

rule rconnect:
  input:
    socket=ancient(rules.start.output.socket)
  shell:
    """
    mysql --socket={input.socket} --database={config[dbname]} \
          -u {sysuser} --password={config[dbrootpass]}
    """
