#!/usr/bin/env python3

include: "common.snake"

rule all:
  input:
    path.tables/"done.load.ncbi_taxonomy"

rule load_all:
  input:
    expand(str(path.tables/"done.load.ncbi_taxonomy.{table}"),
        table=["names", "nodes", "gencode", "division",
               "merged", "delnodes", "citations"]),
  output:
    done=touch(path.tables/"done.load.ncbi_taxonomy")

rule create_table:
  input:
    dbsocket=ancient(path.socket),
    schema=srcdir("../scripts/dbschema/ncbi_taxonomy_db.py"),
    dump=path.tax/"{table}.dmp"
  params:
    table="nt_{table}",
    drop=True
  output:
    done=touch(path.tables/"done.create_tables.ncbi_taxonomy.{table}")
  script: "../scripts/db_create_tables.py"

rule load_data:
  input:
    tsv=path.tax/"{table}.dmp",
    dbsocket=ancient(path.socket),
    done_create_tables=path.tables/"done.create_tables.ncbi_taxonomy.{table}"
  params:
    table="nt_{table}",
    dbschema=srcdir("../scripts/dbschema/ncbi_taxonomy_db.py"),
    ncbidmp=True,
    ignore=True,
    dropkeys=True
  output:
    done=temp(touch(path.tables/"done.load.ncbi_taxonomy.{table}"))
  script: "../scripts/db_load_data.py"

rule get_subtree:
  input:
    rules.create_table.output.done,
    dbsocket=ancient(path.socket)
  params:
    root=config.get("root")
  script: "../scripts/ncbi_taxonomy_db_extract_subtree.py"
